;; The first three lines of this file were inserted by DrScheme. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-intermediate-reader.ss" "lang")((modname student) (read-case-sensitive #t) (teachpacks ()) (htdp-settings #(#t constructor repeating-decimal #f #t none #f ())))
;;==========================================================================
;; homework: structure representing homework points
;; nu: number − the number of the homework
;; points: number − the number of points reached
(define-struct homework (nr points))


;;==========================================================================
;; mid-term: points in one of two mid-term exams
;; nu: number − the number of the mid-term exam
;; points: number − the number of points reached
(define-struct mid-term (nr points))


;;==========================================================================
;; final-lab: points in the final-lab
;; group: number − the number of the student's group
;; points: number − the number of points reached
(define-struct final-lab (group points))


;;==========================================================================
;; student: representing a CS 1 student
;; id: number − unique ID of the student
;; name: string − the name of the student
;; hw: (listof homework) − the student’s homework(s)
;; mid1: mid−term − the student’s result in mid−term A
;; mid2: mid−term − the student’s result in mid−term B
;; lab: final−lab − the student’s results in the finallab
(define-struct student (id name hw mid1 mid2 lab))


;;==========================================================================
;; Test student
(define peter (list 
               4 ;; id
               "Peter Adam" ;; name
               (list (list 1 3) (list 2 4)) ;; listof homework
               (list 1 31) ;; mid-term 1
               (list 2 42) ;; mid-term 2
               (list 10 85))) ;; finallab

(define peter-id-wrong (list 
               'a ;; id
               "Peter Adam" ;; name
               (list (list 1 3) (list 2 4)) ;; listof homework
               (list 1 31) ;; mid-term 1
               (list 2 42) ;; mid-term 2
               (list 10 85))) ;; finallab

(define hans (make-student 3 "Hans Wurst" (list (make-homework 1 23) (make-homework 2 27) (make-homework 3 55)) (make-mid-term 1 80) (make-mid-term 2 20) (make-final-lab 10 100)))


;;==========================================================================
;; Contract: list->student: X -> student
;;        X: (list [id: number]
;;                 [name: string]
;;                 [(list-of-homework): (listof (list number number))]
;;                 [mid-term1: mid-term]
;;                 [mid-term2: mid-term]
;;                 [finallab: final-lab])
;; Purpose: turns a formated list into student-object
;; Example: (list->student 
;;      (list 4 "Peter Adam" (list (list 1 3) (list 2 4)) (list 1 31) (list 2 42) (list 10 85))))
;;      ->  (make-student 4 
;;                        ”Peter Adam” 
;;                        (list (make-homework 1 3)
;;                              (make-homework 2 4))
;;                        (make-mid-term 1 31)
;;                        (make-mid-term 2 42)
;;                        (make-final-lab 10 85))))
(define (list->student lfs) ;; lfs: list-for-student
  (cond 
    [(not (= (length lfs) 6)) (error 'list_format_exeption "wrong number of elements in list")]
    [else 
     (local ((define id (first lfs))
             (define name (second lfs))
             (define homework (parse-homework (third lfs)))
             (define mid1 (parse-mid-term (fourth lfs)))
             (define mid2 (parse-mid-term (fifth lfs)))
             (define finallab (parse-lab (sixth lfs))))
       (cond 
         [(not (number? id)) 
          (error 'list_format_exeption "first element of list not a number")]
         
         [(not (string? name)) 
          (error 'list_format_exeption "second element of list not a string")]
         
         [(string=? name "") 
          (error 'list_format_exeption "second element of list is an empty string")]
         
         [else 
          (make-student id name homework mid1 mid2 finallab)]))]))

;;===Tests: list->student===
(check-expect (list->student peter)(make-student 4 "Peter Adam" (list (make-homework 1 3) (make-homework 2 4)) (make-mid-term 1 31) (make-mid-term 2 42) (make-final-lab 10 85)))
(check-error (list->student peter-id-wrong) "list_format_exeption: first element of list not a number")
(check-error (list->student empty) "list_format_exeption: wrong number of elements in list")


;;==========================================================================
;; Contract: parse-homework: (listof (list number number)) -> (listof homework)
;; Purpose: parses the list of pairs of numbers to a list with homework-structs
;; Example: (parse-homework (list (list 1 2) (list 3 4)))
;;               -> (list (make-homework 1 2) (make-homework 3 4))
(define (parse-homework lopons) ;; lopons: list-of-pair-of-numbers
  (cond
    [(empty? lopons) empty]
    [else (local ((define pon (first lopons)) ;; pon: pair-of-number
                  (define rol (rest lopons))) ;; rol: rest-of-list
            
            (cond
            
              [(not (= (length pon) 2))
               (error 'homwork_list_format_exeption "wrong number of elements")]
              
              [(not (andmap number? pon)) 
               (error 'homwork_list_format_exeption "some elements are no numbers")]
              
              [(not (andmap positive? pon))
               (error 'homwork_list_format_exeption "some numbers are negative")]
              
              [(member (first pon) (map first rol))
               (error 'homwork_list_format_exeption "some homework id's are not unique")]
              
              [else 
               (cons (make-homework (first pon)(second pon)) (parse-homework rol))]))]))

;;===Tests: parse-homework===
(check-expect (parse-homework (list (list 1 2) (list 3 4)(list 5 6)))
              (list (make-homework 1 2)(make-homework 3 4)(make-homework 5 6)))
(check-error (parse-homework (list (list 1 2 3))) 
             "homwork_list_format_exeption: wrong number of elements")
(check-error (parse-homework (list (list 'a 3))) 
             "homwork_list_format_exeption: some elements are no numbers")
(check-error (parse-homework (list (list -1 -3))) 
             "homwork_list_format_exeption: some numbers are negative")
(check-error (parse-homework (list (list 1 2)(list 1 3))) 
             "homwork_list_format_exeption: some homework id's are not unique")


;;==========================================================================
;; Contract: parse-mid-term: (listof number number) -> mid-term
;; Purpose: parses the list of two numbers to a mid-term-struct
;; Example: (parse-mid-term (list 1 2)) -> (make-mid-term 1 2)
(define (parse-mid-term lonn) ;; lonn: list-of-number-number
  (cond [(and (= (length lonn) 2)
              (andmap number? lonn)
              (andmap positive? lonn)
              )
         (make-mid-term (first lonn)(second lonn))
         ]
        [else (error 'list_format_exeption "wrong formated list")]))

;;===Tests: parse-mid-term===
(check-expect(parse-mid-term (list 1 2)) (make-mid-term 1 2))
(check-error (parse-mid-term (list 1)) "list_format_exeption: wrong formated list")
(check-error (parse-mid-term (list 'a 2)) "list_format_exeption: wrong formated list")
(check-error (parse-mid-term (list 1 -2)) "list_format_exeption: wrong formated list")


;;==========================================================================
;; Contract: parse-lab: (listof number number) -> final-lab
;; Purpose: parses the list of two numbers to a final-lab-struct
;; Example: (parse-lab (list 1 2)) -> (make-final-lab 1 2)
(define (parse-lab lonn) ;; lonn: list-of-number-number
  (cond [(and (= (length lonn) 2)
              (andmap number? lonn)
              (andmap positive? lonn)
              )
         (make-final-lab (first lonn)(second lonn))
         ]
        [else (error 'list_format_exeption "wrong formated list")]))

;;===Tests: parse-lab===
(check-expect (parse-lab (list 1 2)) (make-final-lab 1 2))
(check-error (parse-lab (list 1)) "list_format_exeption: wrong formated list")
(check-error (parse-lab (list 'a 2)) "list_format_exeption: wrong formated list")
(check-error (parse-lab (list 1 -2)) "list_format_exeption: wrong formated list")


;;==========================================================================
;; Contract: passed?: student -> boolean
;; Purpose: checks if the student has passed the exam
;; Example: (passed? (list->student peter)) -> false
;; Example: (passed? hans) -> true
(define (passed? student)
  (local ((define sum-hw (sum-homework (student-hw student)))
          (define mid1-points (mid-term-points (student-mid1 student)))
          (define mid2-points (mid-term-points (student-mid2 student)))
          (define lab-points (final-lab-points (student-lab student))))
    
   (cond 
    [(and (>= sum-hw 50)
          (or (>= mid1-points 35)
              (>= mid2-points 35))
          (>= lab-points 50)
          (>= (+ sum-hw 
                 (max mid1-points mid2-points)
                 lab-points) 
              180))
          true]
    [else false])))

;;===Tests: passed?===
(check-expect (passed? (list->student peter)) false)
(check-expect (passed? hans) true)


;;==========================================================================
;; Contract: sum-homework: (listof homework) -> number
;; Purpose: summerize the points reached in homework
;; Example: (sum-homework (list (make-homework 1 25) (make-homework 2 44))) -> 69
(define (sum-homework loh) ;; loh: list-of-homework
  (cond
    [(empty? loh) 0]
  [else (+ 
         (homework-points (first loh))
         (sum-homework (rest loh)))]))

;;===Tests: sum-homework===
(check-expect (sum-homework (list (make-homework 1 25) (make-homework 2 44))) 69)
(check-expect (sum-homework empty) 0)